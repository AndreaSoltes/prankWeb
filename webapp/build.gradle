apply plugin: 'java'
sourceCompatibility = 1.8
targetCompatibility = 1.8
apply plugin: 'war'

war {
    archiveName 'ROOT.war'
}

task('copy_LiteMol_js', type:Copy) {
    // Copy LiteMol plugin (the core).
    from('../LiteMol/build/web/assets/js/') {
        include 'LiteMol-plugin.js'
    }
    // Copy PrankWeb app.
    from ('../LiteMol/build/examples/PrankWeb') {
        include 'LiteMol-example.js'
        rename 'LiteMol-example.js', 'LiteMol-PrankWeb.js'
    }
    into 'src/main/webapp/javascripts'
}
tasks['copy_LiteMol_js'].dependsOn ':LiteMol:gulp_Example-PrankWeb'

task('copy_LiteMol_css', type:Copy) {
    // Copy LiteMol CSS stylesheets.
    from('../LiteMol/build/web/assets/css/') {
        include 'LiteMol-plugin-light.css'
        rename 'LiteMol-plugin-light.css', 'LiteMol-plugin.css'
    }
    into 'src/main/webapp/css'
}
tasks['copy_LiteMol_css'].dependsOn ':LiteMol:gulp_Example-PrankWeb'

processResources.dependsOn 'copy_LiteMol_css'
processResources.dependsOn 'copy_LiteMol_js'

task('deploy', type:Exec) {
    if (System.getProperty('os.name').toLowerCase().contains('windows')) {
        commandLine 'cmd', '/c', "call ${System.env.JBOSS_HOME}\\bin\\jboss-cli.bat --connect controller=localhost \"deploy --force build\\libs\\ROOT.war\""
    } else {
        commandLine "${System.env.JBOSS_HOME}/bin/jboss-cli.sh --connect controller=localhost \"deploy --force build/libs/ROOT.war\""
    }
}

dependencies {
    compile project(':pranklib')

    compile group: 'org.biojava', name: 'biojava-core', version: '4.2.4'
    compile group: 'org.biojava', name: 'biojava-structure', version: '4.2.4'
    compile group: 'org.apache.commons', name: 'commons-configuration2', version: '2.1'
    compile 'commons-beanutils:commons-beanutils:1.9.3'
    compile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.0.1'
    compile group: 'javax', name: 'javaee-api', version: '7.0'

    compile group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'

    //Prank dependencies begin
    compile 'org.codehaus.groovy:groovy-all:2.4.7'
    compile 'org.codehaus.gpars:gpars:1.2.1'
    compile "org.apache.commons:commons-lang3:3.4"
    compile "com.google.guava:guava:19.0"

    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.5'

    compile 'org.biojava:biojava-alignment:4.2.4'

    compile 'org.openscience.cdk:cdk-qsarmolecular:1.5.13'    // for NumericalSurface class
    compile 'org.openscience.cdk:cdk-data:1.5.13'             // for Atom class
    compile 'nz.ac.waikato.cms.weka:weka-dev:3.9.0'
    compile fileTree(dir: 'libs', include: '*.jar')
    // Prank dependencies end
    
}