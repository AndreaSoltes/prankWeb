apply plugin: 'java'
sourceCompatibility = 1.8
targetCompatibility = 1.8
apply plugin: 'war'

war {
    archiveName 'ROOT.war'
}

task('copy_LiteMol_js', type:Copy) {
    // Copy LiteMol plugin (the core).
    from('../LiteMol/build/web/assets/js/') {
        include 'LiteMol-plugin.js'
    }
    // Copy PrankWeb app.
    from ('../LiteMol/build/examples/PrankWeb') {
        include 'LiteMol-example.js'
        rename 'LiteMol-example.js', 'LiteMol-PrankWeb.js'
    }
    into 'src/main/webapp/javascripts'
}
tasks['copy_LiteMol_js'].dependsOn ':LiteMol:gulp_Example-PrankWeb'

task('copy_LiteMol_css', type:Copy) {
    // Copy LiteMol CSS stylesheets.
    from('../LiteMol/build/web/assets/css/') {
        include 'LiteMol-plugin-light.css'
        rename 'LiteMol-plugin-light.css', 'LiteMol-plugin.css'
    }
    into 'src/main/webapp/css'
}
tasks['copy_LiteMol_css'].dependsOn ':LiteMol:gulp_Example-PrankWeb'

task('copy_p2rank_jar', type:Copy) {
    // Copy P2Rank jar file
    from('../p2rank/build/bin/') {
        include 'p2rank.jar'
        rename 'p2rank.jar', 'p2rank_slim.jar'
    }
    into 'libs'
}
tasks['copy_p2rank_jar'].dependsOn ':p2rank:assemble'


// Setup copying p2rank distribution to P2RANK_DISTRO path
task('copy_p2rank_dist', type:Copy) {
    // Copy P2Rank dist to P2RANK_DIST
    from '../p2rank/distro/'
    into "$System.env.P2RANK_DIST"
}
tasks['copy_p2rank_dist'].dependsOn ':p2rank:assemble'

// Check that P2RANK_DISTRO env variable is set.
gradle.taskGraph.whenReady { taskGraph ->
    logger.error(taskGraph.getAllTasks().toString())
    if (taskGraph.hasTask(':webapp:copy_p2rank_dist') && System.getenv('P2RANK_DIST') == null) {
       throw new GradleException("Please export 'P2RANK_DIST' variable, path for p2rank models and configs.")
    }
}

compileJava.dependsOn 'copy_p2rank_jar'
compileJava.dependsOn 'copy_p2rank_dist'
processResources.dependsOn 'copy_LiteMol_css'
processResources.dependsOn 'copy_LiteMol_js'

task('deploy', dependsOn: 'war', type:Exec) {
    if (System.getProperty('os.name').toLowerCase().contains('windows')) {
        commandLine 'cmd', '/c', "call ${System.env.JBOSS_HOME}\\bin\\jboss-cli.bat --connect controller=localhost \"deploy --force build\\libs\\ROOT.war\""
    } else {
        commandLine "./deploy.sh"
    }
    inputs.file "build/libs/ROOT.war"
    outputs.upToDateWhen {true} // Since there is no output.
}

dependencies {
    compile project(':pranklib')

    compile group: 'org.biojava', name: 'biojava-core', version: '4.2.12'
    compile 'org.biojava:biojava-structure:4.2.12'
    compile group: 'org.apache.commons', name: 'commons-configuration2', version: '2.1'
    compile 'commons-beanutils:commons-beanutils:1.9.3'
    compile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.0.1'
    compile group: 'javax', name: 'javaee-api', version: '7.0'

    // Pranklib dependencies
    compile group: 'com.univocity', name: 'univocity-parsers', version: '2.4.0' // For parsing csv
    compile group: 'org.apache.commons', name: 'commons-compress', version: '1.13' // bz2 decompress

    //Prank dependencies begin
    compile 'org.codehaus.groovy:groovy-all:2.5.6'
    compile 'org.codehaus.gpars:gpars:1.2.1'
    compile "org.apache.commons:commons-lang3:3.7"
    compile "commons-io:commons-io:2.6"
    compile "com.google.guava:guava:24.0-jre"
    compile 'com.google.code.gson:gson:2.8.2'
    compile 'org.zeroturnaround:zt-zip:1.12'

    compile 'org.slf4j:slf4j-api:1.7.26'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.11.2'
    compile 'org.slf4j:jul-to-slf4j:1.7.26'                 // for netlib logging messages

    compile 'org.biojava:biojava-alignment:4.2.12'

    compile 'org.openscience.cdk:cdk-qsarmolecular:2.1.1'    // for NumericalSurface class
    compile 'org.openscience.cdk:cdk-data:2.1.1'             // for Atom class
    compile 'nz.ac.waikato.cms.weka:weka-dev:3.9.2'
    compile fileTree(dir: 'libs', include: '*.jar')
    // Prank dependencies end
    
}
